buildscript {
    repositories {
        mavenCentral()
    }
    // durante todos scripts de build é necessário ter isso no classpath
    dependencies {
        classpath('org.flywaydb:flyway-mysql:9.22.0')
    }
}

plugins {
    id 'java'
    id 'application' // torna a aplicação uma "aplicação java" para que possa ser executada como um java app
    id 'org.springframework.boot' version '3.1.2' // inclui o spring framework como plugin (2.6.7 versão usada no curso)
    // e traz com ele as versões das bibliotecas mais recentes e algumas tasks importantes
    id 'io.spring.dependency-management' version '1.1.3' // versão usada no curso 1.0.11.RELEASE
    // inclui as dependências/versões que o spring utiliza automaticamente nas dependências abaixo
    id 'org.flywaydb.flyway' version '9.22.0'
}

group = 'com.fullcyccle.admin.catalogo.infrastructure'
version = '1.0-SNAPSHOT'

// não é uma configuração obrigatória, mas como está o projeto possui módulos e quero que o jar fique na raiz é uma configuração necessária
bootJar {
    archiveFileName = 'application.jar'
    destinationDirectory.set(file("${rootProject.buildDir}/libs"))
}

repositories {
    mavenCentral()
}

dependencies {
    implementation((project(":domain")))
    implementation((project(":application")))

    implementation('com.mysql:mysql-connector-j')

    // configura o módulo web MVC, mas exclui o starter do tomcat
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }

    // undertow será o substituto do tomcat
    implementation('org.springframework.boot:spring-boot-starter-undertow')

    // essa biblioteca que facilita a comunicação com abstrações do spring para persistir informações
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')

    // configura os testes
    testImplementation('org.springframework.boot:spring-boot-starter-test')

    // banco em memória h2 - deveria se testRuntimeOnly mas deu erro - implementation funciona
    testRuntimeOnly('com.h2database:h2')
}

flyway {
    url = System.getenv('FLYWAY_DB') ?: 'jdbc:mysql://localhost:3306/adm_videos'
    user = System.getenv('FLYWAY_USER') ?: 'root'
    password = System.getenv('FLYWAY_PASS') ?: '123456'
}

test {
    useJUnitPlatform()
}